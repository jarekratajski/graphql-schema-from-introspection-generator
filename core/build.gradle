buildscript {
    ext.kotlin_version = '1.2.31'
    ext.dokka_version = '0.9.17'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

description = 'Generate GraphQL Schema (GraphQL DSL or SDL) based on introspection query result'

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.beust:klaxon:3.0.1'
    testCompile "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testCompile "org.junit.jupiter:junit-jupiter-params:5.2.0"
    testCompile 'org.json:json:20180813'
    testCompile 'com.graphql-java:graphql-java:2018-08-23T21-16-30-141b8e4'
    testCompile 'org.skyscreamer:jsonassert:1.5.0'
    ktlint "com.github.shyiko:ktlint:0.27.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/docs/javadoc"
    // suppress internal package
    packageOptions {
        prefix = "io.github.mstachniuk.graphqlschemafromintrospectiongenerator.internal"
        suppress = true
    }
}
javadoc.dependsOn dokka
